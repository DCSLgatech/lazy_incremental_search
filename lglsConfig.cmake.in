###############################################################################
# CMake config file for lgls
#
# This sets the following variables:
# @PROJECT_NAME@_INCLUDE_DIRS - Directories containing the lgls include files.
# @PROJECT_NAME@_LIBRARIES    - Libraries needed to use lgls.
# @PROJECT_NAME@_VERSION      - lgls version.
###############################################################################

@PACKAGE_INIT@

function(lgls_traverse_components output_variable)
  set(output_components)

  foreach(component ${ARGN})
    # We should be able to eliminate the <C>_VISITED property by detecting
    # whether the <C>_FOUND property is DEFINED. Unfortunately, DEFINED checks
    # whether or not define_property() was called, not whether the variable was
    # set. The exact behavior seems to vary between CMake versions, so we use
    # an extra variable instead.
    get_property(is_visited DIRECTORY PROPERTY "lgls_${component}_VISITED")
    set_property(DIRECTORY PROPERTY "lgls_${component}_VISITED" TRUE)

    if(NOT is_visited)
      set(component_include
        "${CMAKE_CURRENT_LIST_DIR}/lgls_${component}Component.cmake")
      set(target_include
        "${CMAKE_CURRENT_LIST_DIR}/lgls_${component}Targets.cmake")

      if(EXISTS "${component_include}" AND EXISTS "${target_include}")
        include("${component_include}")

        set_property(DIRECTORY PROPERTY "lgls_${component}_FOUND" TRUE)
        set_property(DIRECTORY
          PROPERTY "lgls_${component}_INCLUDE_DIRS"
          ${lgls_${component}_INCLUDE_DIRS})
        set_property(DIRECTORY
          PROPERTY "lgls_${component}_DEFINITIONS"
          ${lgls_${component}_DEFINITIONS})
        set_property(DIRECTORY
          PROPERTY "lgls_${component}_LIBRARIES"
          ${lgls_${component}_LIBRARIES})
        set_property(DIRECTORY
          PROPERTY "lgls_${component}_DEPENDENCIES"
          ${lgls_${component}_DEPENDENCIES})

        lgls_traverse_components("${output_variable}" 
          ${lgls_${component}_DEPENDENCIES})

        include("${target_include}")

        # This is not necessary in recent versions of cmake, which supports
        # transitive includes via INTERFACE_INCLUDE_DIRECTORIES.
        foreach(dependency ${lgls_${component}_DEPENDENCIES})
          set_property(DIRECTORY APPEND
            PROPERTY "lgls_${component}_INCLUDE_DIRS"
            ${lgls_${dependency}_INCLUDE_DIRS})
          set_property(DIRECTORY APPEND
            PROPERTY "lgls_${component}_DEFINITIONS"
            ${lgls_${dependency}_DEFINITIONS})
          # LIBRARIES is automatically inherited via INTERFACE_LINK_LIBRARIES.
        endforeach()

        list(APPEND output_components "${component}")
      else()
        set_property(DIRECTORY PROPERTY "lgls_${component}_FOUND" FALSE)
      endif()
    endif()
  endforeach()

  set("${output_variable}" ${output_components} PARENT_SCOPE)
endfunction()

function(lgls_export_variable variable)
  set("${variable}" ${${variable}} CACHE INTERNAL "")
endfunction()

function(lgls_export_property variable)
  get_property(value DIRECTORY PROPERTY "${variable}")
  set("${variable}" ${value} CACHE INTERNAL "")
endfunction()

function(lgls_export_notfound variable)
  set("${variable}" "${variable}-NOTFOUND" CACHE INTERNAL "")
endfunction()

function(lgls_package_init)
  lgls_traverse_components(required_components "${ARGN}")

  set(lgls_INCLUDE_DIRS)
  set(lgls_DEFINITIONS)
  set(lgls_LIBRARIES)

  foreach(component ${ARGN})
    lgls_export_property("lgls_${component}_FOUND")

    if(lgls_${component}_FOUND)
      lgls_export_property("lgls_${component}_INCLUDE_DIRS")
      lgls_export_property("lgls_${component}_DEFINITIONS")
      lgls_export_property("lgls_${component}_LIBRARIES")
      lgls_export_property("lgls_${component}_DEPENDENCIES")

      list(APPEND lgls_INCLUDE_DIRS ${lgls_${component}_INCLUDE_DIRS})
      list(APPEND lgls_DEFINITIONS ${lgls_${component}_DEFINITIONS})
      list(APPEND lgls_LIBRARIES ${lgls_${component}_LIBRARIES})
    else()
      lgls_export_notfound("lgls_${component}_INCLUDE_DIRS")
      lgls_export_notfound("lgls_${component}_DEFINITIONS")
      lgls_export_notfound("lgls_${component}_LIBRARIES")
      lgls_export_notfound("lgls_${component}_DEPENDENCIES")
    endif()
  endforeach()

  lgls_export_variable(lgls_INCLUDE_DIRS)
  lgls_export_variable(lgls_DEFINITIONS)
  lgls_export_variable(lgls_LIBRARIES)
endfunction()

#==============================================================================

# Use find_package_handle_standard_args to generate output. This handles the
# REQUIRED keyword, sets lgls_FOUND, and generates the appropriate STATUS and
# FATAL_ERROR messages. We intentionally do not check lgls_LIBRARIES here
# because it may be empty if the user did not specify any COMPONENTS.
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(lgls
  FOUND_VAR lgls_FOUND
  REQUIRED_VARS lgls_PACKAGE_INCLUDE_DIRS
  HANDLE_COMPONENTS)
